// Copyright 2021 CloudWeGo authors. 
// 
// Licensed under the Apache License, Version 2.0 (the "License"); 
// you may not use this file except in compliance with the License. 
// You may obtain a copy of the License at 
// 
//     http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software 
// distributed under the License is distributed on an "AS IS" BASIS, 
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
// See the License for the specific language governing permissions and 
// limitations under the License. 
// 

package parser


type Function Peg {
}


Function <- '@' Identifier LPAR Arguments RPAR

Arguments <- (ConstValue ListSeparator?)*

ConstValue <- DoubleConstant / IntConstant / Literal / ConstList / ConstMap / FieldReference

FieldReference <- '$' Identifier

IntConstant <- < '0x' ([0-9] / [A-Z] / [a-z])+ / '0o' Digit+ / [+\-]? Digit+ > Skip

DoubleConstant  <- <[+\-]? (
        Digit* '.' Digit+  Exponent?
    /   Digit+ Exponent
    )> Skip

Exponent <- ('e' / 'E') IntConstant

ConstList  <- LBRK (ConstValue ListSeparator?)* RBRK

ConstMap  <- LWING (ConstValue COLON ConstValue ListSeparator?)* RWING

EscapeLiteralChar <- '\\' ["']

Literal <- '"' <(EscapeLiteralChar / !'"' .)*> '"' Skip
        / "'" <(EscapeLiteralChar / !"'" .)*> "'" Skip

Identifier <- <Letter ( Letter / Digit / '.' )*> Skip

ListSeparator <- (',' / ';') Skip

Letter <- [A-Z] / [a-z] / '_'
LetterOrDigit <- [a-z] / [A-Z] / [0-9] / [_$]

Digit <- [0-9]

Skip <- Space*
Space <- [ \t\r\n]+

LBRK        <-  '['     Skip
RBRK        <-  ']'     Skip
LWING       <-  '{'     Skip
RWING       <-  '}'     Skip
EQUAL       <-  '='     Skip
LPOINT      <-  '<'     Skip
RPOINT      <-  '>'     Skip
COMMA       <-  ','     Skip
LPAR        <-  '('     Skip
RPAR        <-  ')'     Skip
COLON       <-  ':'     Skip
